apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

// Load keystore
def keystorePropertiesFile = rootProject.file("../../../vendor/keys/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "com.katsuna.keyboard"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 10
        versionName "1.4.4"
        project.ext.set("archivesBaseName", "Keyboard-" + defaultConfig.versionName)
    }

    signingConfigs {
        katsunaConfig {
            storeFile rootProject.file("../../../vendor/keys/katsuna.keystore")
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        debug {
            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false
        }

        release {
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.katsunaConfig

            lintOptions {
                disable 'MissingTranslation'
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(":commons")
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation 'joda-time:joda-time:2.9.4'
    implementation "com.google.firebase:firebase-core:$firebaseVersion"
    implementation "com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion"

    testImplementation 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'